# Standard library for mshell

# type numeric int | float

def each ([T] (T --) --)
    over len each-len! # Get total length
    0 each-idx! # index
    (
        [(@each-idx @each-len >=) (break)] if
        # "Each idx: " w @each-idx w " Each len: " w @each-len wl
        over @each-idx nth # Get current item
        over x  # Copy over quote, execute
        @each-idx 1 + each-idx! # inc index
        # "Each idx: " w @each-idx wl
    ) loop

    # Drop list and quote, total length, index
    drop drop
end

# filter (list quote -- list)
def filter ([T] (T -- bool) -- [T])
    quote! list!
    @list len filter-len! # Get total length
    0 filter-idx! # Index
    [] filter-accum! # Accumulator
    (
        @filter-idx @filter-len >= (break) iff
        @list @filter-idx nth item!
        # $"Item: {@item str}" wl
        @item @quote x  result!
        # $"Result: {@result str}" wl
        @result ( @filter-accum @item append drop ) iff
        @filter-idx 1 + filter-idx! # inc index
    ) loop
    @filter-accum
end

# foldl (quote initial list -- result)
def foldl ((T T -- T) T [T] -- T)
    #  quote initial list
    swap foldl-accum!  # Accumulator,
    # quote list
    swap foldl-quote! # Quote
    # list
    (
        @foldl-accum swap # Accumulator item
        @foldl-quote x # Execute quote
        foldl-accum! # Update accumulator
    ) each

    @foldl-accum
end

# sum (list -- value)
def sum ([float] -- float)
    (+) 0 rot foldl
end

# [s]tdin [l]ines
def sl (-- [str]) stdin lines end

def ttFile (str -- [[str]])
    readFile lines (tab split) map
end

# wt = Whitespace separated Table (-- [[str*]*])
def wt (-- [[str]]) sl (wsplit) map  end

def wtFile (str -- [[str]])
    readFile lines (wsplit) map
end

# wjoin = Whitespace join ([str*] -- str)
def wjoin ([str] -- str) " " join end

# unlines (list[str] -- str)
def unlines ([str] -- str)
    [] unlines-accum! # Accumulator
    (
       @unlines-accum swap append
       "\n" append
       drop
    ) each
    @unlines-accum "" join
end

# reverse ([T*]|str -- [T*]|str)
def reverse ([T] -- [T])
    dup len 1 - reverse-idx! # Set Idx
    [] # list accum
    (
        [(@reverse-idx 0 <) (break)] if
        over @reverse-idx nth append # Get current item
        @reverse-idx 1 - reverse-idx! # dec index
    ) loop
    nip # Drop original list
end

# abs (int|float -- int|float)
def abs (float -- float)
    [(dup 0 <) (-1 *)] if
end


def tab (-- str) "	" end
def tsplit (str -- [str])  tab split end
def tjoin ([str] -- str)  tab join end

def cjoin ([str] -- str) "," join end

def uw ([str] --) unlines w end
# tuw = tab unlines write
def tuw ([[str]] -- ) (tjoin) map uw end

def readTsvFile (str -- [[str]])
    readFile lines (tsplit) map
end

def max2 (float float -- float)
    over over [(<) (swap)] if drop
end

# TODO: define actual float/int min
def max ([float] -- float)
    (max2) -999999999999 rot foldl
end

def min2 (float float -- float)
    over over [(>) (swap)] if drop
end

# TODO: define actual float/int max
def min ([float] -- float)
    (min2) 999999999999 rot foldl
end

# This also works for non-square matrices, swapping row-major for column-major
# Horribly inefficient, but it works
def transpose ([[a]] -- [[a]])
    list! @list (len) map max colmax!
    @list len numrows!
    [] transpose-accum!
    0 colIndex!
    (
        [(@colIndex @colmax >=) (break)] if
        0 rowIndex!
        [] inner-accum!
        (
            [(@rowIndex @numrows >=) (break)] if
            @list @rowIndex nth curRow!
            [
                (@curRow len @colIndex >)
                (@inner-accum @curRow @colIndex nth append drop)
            ] if
            @rowIndex 1 + rowIndex!
        ) loop

        @transpose-accum @inner-accum append drop
        @colIndex 1 + colIndex!
    ) loop

    @transpose-accum
end

def any ([T] (T -- bool) -- bool)
    quote! list!
    0 index!
    @list len listLength!
    false result!
    (
        @index @listLength >=
        (break)
        (
            @list @index nth @quote x
            (true result! break)
            ()
            iff
        )
        iff
        @index 1 + index!
    ) loop
    @result
end

def all ([T] (T -- bool) -- bool)
    quote! (@quote x not) any not
end

def isoDateFmt (date -- str)
    "2006-01-02" dateFmt
end

def isoDateTimeFmt (date -- str)
    "2006-01-02T15:04" dateFmt
end

def zip ([a] [b] (a b -- c) -- [c])
    quote! list2! list1!
    @list1 len @list2 len min2 length!
    0 index!
    [] accum!
    (
        @index @length >= (break) iff
        @accum
        @list1 @index nth
        @list2 @index nth
        @quote x
        append
        drop
        @index 1 + index!
    ) loop
    @accum
end

# Builds a sequence of length of input, starting at 0.
def seq (int -- [int])
    max!
    0 current!
    [] seq-accum! # Accumulator
    (
        @current @max >= (break) iff
        @seq-accum @current append drop
        @current 1 + current!
    ) loop

    @seq-accum
end

def htmlDescendents (dict -- [dict])
    [] htmlDescendentsAcc
end

# Accumulator version
def htmlDescendentsAcc (dict [dict] -- [dict])
    accum! node!
    @node "children" get? (c! @c @accum htmlDescendentsAcc drop) each
    @accum @node append
end

def findByTag (dict str -- [dict])
  tag! htmlDescendents ("tag" get (@tag =) map false maybe) filter
end
